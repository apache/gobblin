/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * The motivation of having separated dependency module for orc-mapreduce is due to
 * standalone orc-core libaray depends on much higher version of hive which could conflict
 * with very lower version of hive-exec-core used by gobblin.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
tasks.remove(tasks.uploadShadow)

dependencies {
  compile externalDependency.orcMapreduce
}

configurations {
  compile {
    transitive = true
  }
  compile {
    exclude group: "commons-cli"
    exclude group: "org.apache.avro"
    exclude group: "org.apache.httpcomponents"
    exclude group: "org.apache.hadoop"
  }
}

shadowJar {
  zip64 true
  classifier ""

  dependencies {
    exclude dependency('org.slf4j:.*')
    exclude dependency('commons-lang:.*')
  }

  relocate 'org.apache.hadoop.hive', 'shadow.gobblin.orc.org.apache.hadoop.hive'
  relocate 'com.google', 'shadow.gobblin.orc.com.google'
}

publishing {
  publications {
    shadowPub(MavenPublication) {
      //from components.java
      artifacts = [artifact(tasks.shadowJar)]

      pom pomAttributes
      pom.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')

        configurations.runtime.allDependencies.each {
          if (it.group != null && it.name != null) {
            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', it.group)
            dependencyNode.appendNode('artifactId', it.name)
            dependencyNode.appendNode('version', it.version)

            if (it.excludeRules.size() > 0) {
              def exclusionsNode = dependencyNode.appendNode('exclusions')
              it.excludeRules.each { rule ->
                def exclusionNode = exclusionsNode.appendNode('exclusion')
                exclusionNode.appendNode('groupId', rule.group)
                exclusionNode.appendNode('artifactId', rule.module)
              }
            }
          }
        }
      }
    }
  }
}
addPublicationToBintray("shadowPub")

ext.classification="library"
