// Copyright (C) 2014-2015 LinkedIn Corp. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License"); you may not use
// this file except in compliance with the License. You may obtain a copy of the
// License at  http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied.

apply plugin: 'pegasus'
apply plugin: 'java'

repositories {
  mavenCentral()
  maven {
    url "http://conjars.org/repo"
  }
  maven {
    url "https://repository.cloudera.com/artifactory/cloudera-repos/"
  }
}

dependencies {
    dataModel externalDependency.pegasus.restliCommon
    compile externalDependency.pegasus.data
    compile externalDependency.pegasus.restliClient
    compile externalDependency.pegasus.restliCommon
    compile externalDependency.pegasus.pegasusCommon
}

artifacts {
  archives mainRestClientJar
  archives mainDataTemplateJar
} 

 
configure(install.repositories.mavenInstaller) {
  addFilter('gobblin-rest-api-rest-client') {artifact, file ->
    artifact.name == 'gobblin-rest-api-rest-client'
  }.artifactId = 'gobblin-rest-api-rest-client'

  addFilter('gobblin-rest-api-data-template') {artifact, file ->
    artifact.name == 'gobblin-rest-api-data-template'
  }.artifactId = 'gobblin-rest-api-data-template'

  addFilter('gobblin-rest-api') {artifact, file ->
    artifact.name == 'gobblin-rest-api'
  }.artifactId = 'gobblin-rest-api'  
  // artifact names for 'data-model', 'avro-schema' and 'rest-model' may be added as well if needed
}

// Configure sources and javadoc jars for the data template library
def dataTemplateName = project.name + "-data-template"

task dataTemplateSourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.mainGeneratedDataTemplate.allSource
    classifier = 'sources'
    baseName = dataTemplateName
}

task dataTemplateJavadoc(type: Javadoc, dependsOn: classes) {
    source sourceSets.mainGeneratedDataTemplate.allSource
    classpath = files(sourceSets.mainGeneratedDataTemplate.compileClasspath)
    destinationDir file("${rootProject.buildDir}/${dataTemplateName}/docs/javadoc")
}
//javadoc.dependsOn dataTemplateJavadoc

task dataTemplateJavadocJar(type: Jar, dependsOn: 'dataTemplateJavadoc') {
    from dataTemplateJavadoc
    baseName = dataTemplateName
    classifier = 'javadoc'
}


// Configure sources and javadoc jars for the rest client  library
def restClientName = project.name + "-rest-client"

task restClientSourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.mainGeneratedRest.allSource
    classifier = 'sources'
    baseName = restClientName
}

task restClientJavadoc(type: Javadoc, dependsOn: classes) {
    source sourceSets.mainGeneratedRest.allSource
    classpath = files(sourceSets.mainGeneratedRest.compileClasspath)
    destinationDir file("${rootProject.buildDir}/${restClientName}/docs/javadoc")
}
//javadoc.dependsOn dataTemplateJavadoc

task restClientJavadocJar(type: Jar, dependsOn: 'restClientJavadoc') {
    from restClientJavadoc
    baseName = restClientName
    classifier = 'javadoc'
}

artifacts { 
    archives dataTemplateSourcesJar, dataTemplateJavadocJar, restClientSourcesJar, restClientJavadocJar 
}

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath group: 'com.linkedin.pegasus', name: 'gradle-plugins', version: '1.15.9'
    }
}
