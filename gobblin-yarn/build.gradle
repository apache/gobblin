// (c) 2014 LinkedIn Corp. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License"); you may not use
// this file except in compliance with the License. You may obtain a copy of the
// License at  http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied.
//

apply plugin: 'java'

repositories {
  mavenCentral()
  maven {
    url "http://conjars.org/repo"
  }
}

dependencies {
  compile project(":gobblin-api")
  compile project(":gobblin-core")
  compile project(":gobblin-metrics")
  compile project(":gobblin-metastore")
  compile project(":gobblin-runtime")
  compile project(":gobblin-utility")
  compile project(":gobblin-scheduler")
  compile project(path: ':gobblin-rest-service:gobblin-rest-api', configuration: 'restClient')

  compile externalDependency.avro
  compile externalDependency.commonsConfiguration
  compile externalDependency.quartz
  compile externalDependency.guava
  compile externalDependency.commonsLang
  compile externalDependency.slf4j
  compile externalDependency.log4j
  compile externalDependency.commonsCli
  compile externalDependency.gson
  compile externalDependency.metricsCore
  compile externalDependency.metricsJvm
  compile externalDependency.commonsIo
  compile externalDependency.commonsEmail
  compile externalDependency.pegasus.data
  compile externalDependency.findBugs
  compile externalDependency.typesafeConfig
  compile externalDependency.hadoopCommon
  compile externalDependency.hadoopYarnApi
  compile externalDependency.hadoopYarnCommon
  compile externalDependency.hadoopYarnClient
  if (project.hasProperty('useHadoop2')) {
    compile externalDependency.avroMapredH2
  } else {
    compile externalDependency.avroMapredH1
  }

  testCompile project(":gobblin-example")

  testCompile externalDependency.testng
  testCompile externalDependency.hadoopYarnMiniCluster
  testCompile externalDependency.curatorFramework
  testCompile externalDependency.curatorTest

  compile files('./src/main/resources/helix-core-0.6.6-SNAPSHOT.jar')
}

task testJar(type: Jar, dependsOn: testClasses) {
  baseName = "test-${project.archivesBaseName}"
  from sourceSets.test.output
}

configurations {
  tests
}

configurations {
  compile {
    transitive = false
  }

  testRuntime {
    resolutionStrategy {
      force 'com.google.inject:guice:3.0'
      force 'org.apache.hadoop:hadoop-yarn-server-resourcemanager:'+hadoopVersion
    }
  }
}

artifacts {
  tests testJar
}

test {
  useTestNG () {
    excludeGroups 'ignore'
    if (project.hasProperty('skipTestGroup')) {
      excludeGroups skipTestGroup
    }
  }
  workingDir rootProject.rootDir
  maxParallelForks = 1
}

clean {
  delete "../gobblin-test/locks"
  delete "../gobblin-test/basicTest"
}

classification="library"
